/*
 * Copyright (c) 2022 VMware Inc. or its affiliates, All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import me.champeau.gradle.japicmp.JapicmpTask

apply plugin: 'biz.aQute.bnd.builder'
apply plugin: 'jcstress'
apply plugin: 'java-library'

description = 'Reactor Generic Object Pooling'

ext {
	shortName = 'Reactor Pool'
	osgiVersion = osgiVersion(version.toString())
	bndOptions = [
		"Export-Package": [
			"!*internal*",
			"reactor.pool.*;version=$osgiVersion"
		].join(","),
		"Import-Package": [
			"!javax.annotation",
			"*"
		].join(","),
		"Bundle-Name" : "reactor-pool",
		"Bundle-SymbolicName" : "io.projectreactor.addons.reactor-pool",
		"Bundle-Version" : "$osgiVersion"
	]
}

dependencies {
	api libs.reactor.core

	// JSR-305 annotations
	compileOnly libs.jsr305

	// Testing
	testImplementation libs.assertj
	testImplementation libs.awaitility
	testImplementation libs.hdrHistogram
	testRuntimeOnly libs.slf4j.jcl
	testRuntimeOnly libs.logback
	testImplementation libs.mockito

	testImplementation libs.reactor.test
	testImplementation libs.reactor.extra

	testImplementation libs.rxjava2

	testImplementation platform(libs.junit.bom)
	testImplementation "org.junit.jupiter:junit-jupiter-api"
	testImplementation "org.junit.jupiter:junit-jupiter-params"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
}

task downloadBaseline(type: Download) {
	onlyIfNewer true
	compress true

	src "${repositories.mavenCentral().url}io/projectreactor/addons/reactor-pool/${libs.versions.baseline.pool.api.get()}/reactor-pool-${libs.versions.baseline.pool.api.get()}.jar"
	dest "${buildDir}/baselineLibs/reactor-pool-${libs.versions.baseline.pool.api.get()}.jar"
}

def japicmpReport = tasks.register('japicmpReport') {
	onlyIf {
		japicmp.state.failure != null
	}
	doLast {
		def reportFile = file("${project.buildDir}/reports/japi.txt")
		if (reportFile.exists()) {
			println "\n **********************************"
			println " * /!\\ API compatibility failures *"
			println " **********************************"
			println "Japicmp report was filtered and interpreted to find the following incompatibilities:"
			reportFile.eachLine {
				if (it.contains("*") && (!it.contains("***") || it.contains("****")))
					println "source incompatible change: $it"
				else if (it.contains("!"))
					println "binary incompatible change: $it"
			}
		}
		else println "No incompatible change to report"
	}
}

task japicmp(type: JapicmpTask) {
	if (project.gradle.startParameter.isOffline()) {
		println "Offline: skipping downloading of baseline and JAPICMP"
		enabled = false
	}
	else if ("${libs.versions.baseline.pool.api.get()}" == "SKIP") {
		println "SKIP: Instructed to skip the baseline comparison"
		enabled = false
	}
	else {
		println "Will download and perform baseline comparison with ${libs.versions.baseline.pool.api.get()}"
		dependsOn(downloadBaseline)
		finalizedBy(japicmpReport)
	}

	oldClasspath.from(tasks.downloadBaseline.outputs.files)
	newClasspath.from(tasks.jar.outputs.files)
	// these onlyXxx parameters result in a report that is slightly too noisy, but better than
	// onlyBinaryIncompatibleModified = true which masks source-incompatible-only changes
	onlyBinaryIncompatibleModified = false
	onlyModified = true
	failOnModification = true
	failOnSourceIncompatibility = true
	txtOutputFile = file("${project.buildDir}/reports/japi.txt")
	ignoreMissingClasses = true
	includeSynthetic = true
	compatibilityChangeExcludes = [ "METHOD_NEW_DEFAULT" ]

	//TODO after a release, bump the gradle.properties baseline
	//TODO after a release, remove the reactor-pool exclusions below if any
	classExcludes = [
	]
	methodExcludes = [
			"reactor.pool.decorators.InstrumentedPoolDecorators#concurrentPools(int, org.reactivestreams.Publisher, java.util.function.Function)"
	]
}
check.dependsOn japicmp

jar {
	manifest {
		attributes("Created-By": "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})",
			"Implementation-Title": project.name,
			"Implementation-Version": project.version,
			"Automatic-Module-Name": "reactor.pool")
	}
	bnd(bndOptions)
}

jcstress {
	mode = 'default' //quick, default, tough
}
tasks.jcstressJar.classifier("jcstress") // make sure jcstress jar doesn't override our own
tasks.check.dependsOn(tasks.jcstress)

check.dependsOn jacocoTestReport